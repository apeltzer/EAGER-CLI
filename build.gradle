group 'com.uni-tuebingen.de.it.eager.eager-cli'

version '1.92.55'


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}


allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.jfrog.bintray'
}

sourceCompatibility = 1.8


sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}


dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'com.google.guava:guava:19.0'
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.9'
    compile 'com.uni-tuebingen.de.it.eager:EAGER-lib:+'
}

jar {
    manifest {
        attributes("Implementation-Title": "EAGER",
                "Implementation-Version": version, "main-Class": "Runner.RunEAGER")
    }
    doFirst {
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}


publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.uni-tuebingen.de.it.eager'
            artifactId 'EAGER-CLI'
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    publish = true
    override = true
    pkg {
        repo = 'EAGER'
        name = 'EAGER-CLI'
        licenses = ['GPL-3.0']
        vcsUrl = "https://github.com/apeltzer/EAGER-CLI"
        version {
            name = project.version
            desc = 'The EAGER Command Line Component. This is the main execution runner required to run XML files created with the GUI component.'
            released  = new Date()
            vcsTag = project.version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }

    }
}
